-- @atlcompiler emftvm
-- @nsURI UML2=http://www.eclipse.org/uml2/3.0.0/UML
-- $Id$
-- Implements the Applet profile using an SWT application
module UML2SWTApplication;

create OUT : UML2 from IN : UML2, SWTAPP : UML2, IMPLTYPES : UML2;

uses "lib::UML2";
uses "lib::Mappings";
uses UML2Copy;

-- ======================================================================
-- model-specific helpers begin
-- ======================================================================

helper def : language : String = 'language'.value();
helper def : SWTApplication : UML2!"uml::Class" = 'swt::SWTApplication'.class();
helper def : implStringA : UML2!"uml::Class" = ('String'.implTypeName() + '[]').class();

helper context UML2!"uml::Classifier" def : isApplet : Boolean =
	not self.getAppliedStereotype('Applet::Applet').oclIsUndefined();

helper context UML2!"uml::Classifier" def : isSingleton : Boolean =
	not self.getAppliedStereotype('Singleton::Singleton').oclIsUndefined();

-- ======================================================================
-- model-specific helpers end
-- ======================================================================

-- ======================================================================
-- transformation rules begin
-- ======================================================================

rule Class {
    from s : UML2!"uml::Class" in IN (
		s.oclIsTypeOf(UML2!"uml::Class"))
    to t : UML2!"uml::Class" (
        __xmiID__ <- s.__xmiID__,
        name <- s.name,
        visibility <- s.visibility,
        isLeaf <- s.isLeaf,
        isAbstract <- s.isAbstract,
        isActive <- s.isActive,
        eAnnotations <- s.eAnnotations,
        ownedComment <- s.ownedComment,
        clientDependency <- s.clientDependency,
        nameExpression <- s.nameExpression,
        elementImport <- s.elementImport,
        packageImport <- s.packageImport,
        ownedRule <- s.ownedRule,
        templateParameter <- s.templateParameter,
        templateBinding <- s.templateBinding,
        ownedTemplateSignature <- s.ownedTemplateSignature,
        generalization <- s.generalization,
        powertypeExtent <- s.powertypeExtent,
        redefinedClassifier <- s.redefinedClassifier,
        substitution <- s.substitution,
        representation <- s.representation,
        collaborationUse <- s.collaborationUse,
        ownedUseCase <- s.ownedUseCase,
        useCase <- s.useCase,
        ownedAttribute <- s.ownedAttribute,
        ownedConnector <- s.ownedConnector,
        ownedBehavior <- s.ownedBehavior,
        classifierBehavior <- s.classifierBehavior,
        interfaceRealization <- s.interfaceRealization,
        ownedTrigger <- s.ownedTrigger,
        nestedClassifier <- s.nestedClassifier,
        ownedOperation <- s.ownedOperation,
        ownedReception <- s.ownedReception)
}
	
rule AppletClass extends Class {
    from s : UML2!"uml::Class" in IN (
		s.isApplet)
	using { hasInit : Boolean = s.ownedOperation->exists(o|o.name='init'); }
    to t : UML2!"uml::Class" (
        name <- s.name.debug('AppletClass'),
        ownedBehavior <- s.ownedBehavior
			->union(
        		if hasInit then Sequence{mainBehavior}
        		else Sequence{mainBehavior, thisModule.AppletInitOperation(s)} endif)),
	-- SWT application functionality --
	   g : UML2!"uml::Generalization" (
	   	general <- thisModule.SWTApplication,
		specific <- s),
	   main : UML2!"uml::Operation" (
	   	name <- 'main',
		class <- s,
		visibility <- #public,
		concurrency <- #sequential,
		isStatic <- true,
		ownedParameter <- Sequence{mainArgs}),
	   mainArgs : UML2!"uml::Parameter" (
	   	name <- 'args',
		type <- thisModule.implStringA,
		effect <- #read,
		direction <- #"in"),
	   mainBehavior : UML2!"uml::OpaqueBehavior" (
	   	specification <- main,
		name <- 'mainBehavior',
		language <- Sequence{thisModule.language},
		body <- Sequence{s.name.swtAppMainBody(s.isSingleton)})
}

lazy rule AppletInitOperation {
	from s : UML2!"uml::Class"
	to initOp : UML2!"uml::Operation" (
	   	name <- 'init',
		class <- s.debug('AppletInitOperation'),
		visibility <- #public,
		concurrency <- #sequential),
	   initBehavior : UML2!"uml::OpaqueBehavior" (
	   	specification <- initOp,
		name <- initOp.name + 'Behavior',
		language <- Sequence{thisModule.language},
		body <- Sequence{s.isSingleton.swtAppInitBody()})
	do {
		initBehavior;
	}
}

rule OpaqueBehavior {
    from s : UML2!"uml::OpaqueBehavior" in IN (
		s.oclIsTypeOf(UML2!"uml::OpaqueBehavior"))
	using { isAppletInit : Boolean = 
		s.specification.name = 'init' and
		s.specification.class.isApplet; }
    to t : UML2!"uml::OpaqueBehavior" (
        __xmiID__ <- s.__xmiID__,
        name <- s.name,
        visibility <- s.visibility,
        isLeaf <- s.isLeaf,
        isAbstract <- s.isAbstract,
        isActive <- s.isActive,
        isReentrant <- s.isReentrant,
        body <-
			if isAppletInit then
				s.body->collect(b|s.specification.class
					.isSingleton.swtAppInitBody() + '\n' + b)
			else
				s.body
			endif,
        language <- s.language,
        eAnnotations <- s.eAnnotations,
        ownedComment <- s.ownedComment,
        clientDependency <- s.clientDependency,
        nameExpression <- s.nameExpression,
        elementImport <- s.elementImport,
        packageImport <- s.packageImport,
        ownedRule <- s.ownedRule,
        templateParameter <- s.templateParameter,
        templateBinding <- s.templateBinding,
        ownedTemplateSignature <- s.ownedTemplateSignature,
        generalization <- s.generalization,
        powertypeExtent <- s.powertypeExtent,
        redefinedClassifier <- s.redefinedClassifier,
        substitution <- s.substitution,
        representation <- s.representation,
        collaborationUse <- s.collaborationUse,
        ownedUseCase <- s.ownedUseCase,
        useCase <- s.useCase,
        ownedAttribute <- s.ownedAttribute,
        ownedConnector <- s.ownedConnector,
        ownedBehavior <- s.ownedBehavior,
        classifierBehavior <- s.classifierBehavior,
        interfaceRealization <- s.interfaceRealization,
        ownedTrigger <- s.ownedTrigger,
        nestedClassifier <- s.nestedClassifier,
        ownedOperation <- s.ownedOperation,
        ownedReception <- s.ownedReception,
        redefinedBehavior <- s.redefinedBehavior,
        ownedParameter <- s.ownedParameter,
        precondition <- s.precondition,
        postcondition <- s.postcondition,
        ownedParameterSet <- s.ownedParameterSet,
        specification <- s.specification)
}

-- ======================================================================
-- transformation rules end
-- ======================================================================
